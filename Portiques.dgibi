*************************************************
* Options générales
*************************************************
OPTI DIME 2 ELEM TRI3 MODE PLAN CONT;
OPTI EPSILON LINEAIRE;
DENS 0.025;

*************************************************
* Parametres geometriques
*************************************************
H1 = 0.2;
H2 = 0.2;
H3 = 0.2;
H4 = 1.0;
H5 = 1.0;
H6 = 1.0;

* Masse étage 1
Met1 = 7.5e3;

* Masse étage 2
Met2 = 7.5e3;

*************************************************
* Parametres matériaux
*************************************************

* Module d'Young
Ecol   = 36000e6;
Epou   = 28000e6;

* Coefficient de Poission
NUcol  = 0.2;
NUpou  = 0.2;

* Masse volumique
RHOpor = 2500;

*************************************************
* Maillage
*************************************************
* Points
P1  = (H1) (0);
P2  = (0)  (0);
P3  = (0)  (H3 + H4 + H2 + H5);
P4  = (H1 + H6 + H1) (H3 + H4 + H2 + H5);
P5  = (H1 + H6 + H1) (0);
P6  = (H1 + H6) (0);
P7  = (H1 + H6) (H5);
P8  = (H1) (H5);
P9  = (H1) (H5 + H2);
P10 = (H1 + H6) (H5 + H2);
P11 = (H1 + H6) (H5 + H2 + H4);
P12 = (H1) (H5 + H2 + H4);
P13 = (H1) (H3 + H4 + H2 + H5);
P14 = (H1 + H6) (H3 + H4 + H2 + H5);

* Droites
L1  = DROI P1  P2;
L2  = DROI P2  P3;
L3  = DROI P3  P13;
L4  = DROI P13 P12;
L5  = DROI P12 P9;
L6  = DROI P9  P8;
L7  = DROI P8  P1;
LT1 = L1 ET L2 ET L3 ET L4 ET 
      L5 ET L6 ET L7;

L8  = DROI P14 P4;
L9  = DROI P4  P5;
L10 = DROI P5  P6;
L11 = DROI P6  P7;
L12 = DROI P7  P10;
L13 = DROI P10 P11;
L14 = DROI P11 P14;
LT2 = L8 ET L9 ET L10 ET L11 ET 
      L12 ET L13 ET L14;

L15 = DROI P13 P14;
L16 = DROI P11 P12;
LT3 = L15 ET L14 ET L16 ET L4;

L17 = DROI P9 P10;
L18 = DROI P8 P7;
LT4 = L17 ET L12 ET L18 ET L6;

* Surfaces
S1 = SURF LT1 PLAN;
S1 = S1 COUL ROUG;
S1 = ORIE S1;
S2 = SURF LT2 PLAN;
S2 = S2 COUL VERT;
S2 = ORIE S2;
S3 = SURF LT3 PLAN;
S3 = S3 COUL JAUN;
S3 = ORIE S3;
S4 = SURF LT4 PLAN;
S4 = S4 COUL CYAN;
S4 = ORIE S4;
STOT = S1 ET S2 ET S3 ET S4;
STOT = ORIE STOT;
OPTI SORT 
'C:\Users\BR232186\Desktop\CastLab_OOP\DEV_SYNC\portique.mail';
SORT STOT;
TRAC STOT;
OPTI DONN 5;
FIN;
*************************************************
* Modele
*************************************************
MO1 = MODE S1 MECANIQUE ELASTIQUE ISOTROPE;
MO2 = MODE S2 MECANIQUE ELASTIQUE ISOTROPE;
MO3 = MODE S3 MECANIQUE ELASTIQUE ISOTROPE;
MO4 = MODE S4 MECANIQUE ELASTIQUE ISOTROPE;	  
MOTOT = MO1 ET MO2 ET MO3 ET MO4;

*************************************************
* Materiau
*************************************************
MA1 = MATE MO1 YOUN Ecol NU NUcol RHO RHOpor;
MA2 = MATE MO2 YOUN Ecol NU NUcol RHO RHOpor;
MA3 = MATE MO3 YOUN Epou NU NUpou RHO RHOpor;
MA4 = MATE MO4 YOUN Epou NU NUpou RHO RHOpor;
MATOT = MA1 ET MA2 ET MA3 ET MA4;

*************************************************
* Conditions aux limites
*************************************************
* Encastrement - encastrement
*CL1 = BLOQ L1  DEPL;
*CL2 = BLOQ L10 DEPL;

* Rotule - rotule
*CL1 = BLOQ P2 DEPL;
*CL2 = BLOQ P6 DEPL;

* Rotule - appui simple
CL1 = BLOQ P2 DEPL;
CL2 = BLOQ P6 UY;


CLTOT = CL1 ET CL2;

*************************************************
* Matrice de rigidité
*************************************************
RIG1 = RIGI MOTOT MATOT;
RIG1 = RIG1 ET CLTOT;
			   
*************************************************
* Matrice de masse
*************************************************
MAS1 = MASS MOTOT MATOT;
MAD1 = MASS DEPL (Met1 / (NBNO L15)) L15;
MAD2 = MASS DEPL (Met2 / (NBNO L17)) L17;
MAS1 = MAS1 ET MAD1 ET MAD2;			   

*************************************************
* Analyse modale
*************************************************
* Calcul
TABMOD = VIBR 'INTERVALLE' 0 100 
         RIG1 MAS1 IMPR TBAS FAUX;

* Post traitement simple		 
NBmode = (DIME TABMOD . MODES) - 2;
REPE BOU1 NBmode;
I = &BOU1;
DEFM = TABMOD . MODES . I . DEFORMEE_MODALE;
DEFI = DEFO Stot DEFM;
TITRE (CHAI 'Frequence (Hz) : ' 
TABMOD . MODES . I . FREQUENCE);
TRAC DEFI (TABMOD . MODES . I . DEFORMEE_MODALE);
TITRE (CHAI 'Frequence (Hz) : ' 
TABMOD . MODES . I . FREQUENCE);
TRAC MOTOT (EPSI MOTOT (TABMOD . MODES . I . DEFORMEE_MODALE));

FIN BOU1;

*************************************************
* Acquisition signaux
*************************************************
OPTI ACQU 
(CHAI Ô009_AxTab.txt');
ACQU ACT*LISTREEL 9211;

OPTI ACQU 
(CHAI 009_time.txt');
ACQU TIME*LISTREEL 9211;

EVT = EVOL MANU TIME ACT;
DESS EVT;

*************************************************
* Chargement dynamique
*************************************************
CH1 = MANU CHPO STOT 1 UX 1.0;
FO1 = -1.0 * MAS1 * CH1;
CH1 = CHAR MECA FO1 EVT;


*************************************************
* Calcul sur base modale
*************************************************

* Chargement
CHMOD = PJBA CH1 TABMOD;

TABCHAR = TABLE 'CHARGEMENT' ;
TABCHAR.'BASE_A' = CHMOD  ;

* Table en sortie de DYNE
TABSOR = TABLE 'SORTIE' ;
TABVAR = TABLE 'VARIABLE' ;

TABSOR.'VARIABLE' = TABVAR ;
TABVAR.'DEPLACEMENT' = VRAI ;
TABVAR.'VITESSE' = VRAI   ;
TABVAR.'ACCELERATION' = VRAI ;

* Definition de la table d'amortissement
TAMOR = TABLE AMORTISSEMENT;
TAMOR . AMORTISSEMENT = AMOR TABMOD (PROG NBmode*10);

* Appel à DYNE
TABRES = DYNE DE_VOGELAERE TABMOD TABCHAR 
TABSOR TAMOR 9210 0.000977;

* Recombinaison modale
EVP12A = EVOL ROUG RECO TABRES TABMOD CH1 'ACCE' P12 UX ; 
EVP11A = EVOL ROUG RECO TABRES TABMOD CH1 'ACCE' P11 UX ; 
EVP09A = EVOL ROUG RECO TABRES TABMOD CH1 'ACCE' P9  UX ; 
EVP10A = EVOL ROUG RECO TABRES TABMOD CH1 'ACCE' P10 UX ; 

EVP12D = EVOL ROUG RECO TABRES TABMOD CH1 'DEPL' P12 UX ; 
EVP11D = EVOL ROUG RECO TABRES TABMOD CH1 'DEPL' P11 UX ; 
EVP09D = EVOL ROUG RECO TABRES TABMOD CH1 'DEPL' P9  UX ; 
EVP10D = EVOL ROUG RECO TABRES TABMOD CH1 'DEPL' P10 UX ; 

EVP12A = EVP12A + EVT;
EVP11A = EVP11A + EVT;
EVP09A = EVP09A + EVT;
EVP10A = EVP10A + EVT;

SP12A = SPO EVP12A 'AMOR' (PROG 0.05) 
'FREQ' (PROG 0 PAS 0.01 100) 'ACCE';
SP11A = SPO EVP11A 'AMOR' (PROG 0.05) 
'FREQ' (PROG 0 PAS 0.01 100) 'ACCE';
SP09A = SPO EVP09A 'AMOR' (PROG 0.05) 
'FREQ' (PROG 0 PAS 0.01 100) 'ACCE';
SP10A = SPO EVP10A 'AMOR' (PROG 0.05) 
'FREQ' (PROG 0 PAS 0.01 100) 'ACCE';

DESS ((EVP12A COUL VERT) ET (EVP11A COUL ROUG)); 
DESS ((SP12A COUL VERT) ET (SP11A COUL ROUG)); 

FIN;




